# data definition
List: {                            # A list is either
    Null: ()                       # empty or
    Pair: (first :Obj, rest :List) # a first element followed by a rest-list.
} datadef

# Turns an array into a list.
tolist: (a :Arr -> :List) {
    null a reverse { swap pair } for
} fun

# Turns a list into a string.
tostr: (l :List -> :Str) {
    { l null? } { "" }
    { l pair-rest null? } { l pair-first str }
    { true } { 
        l pair-first str " " +  
        l pair-rest recur + 
    }
} cond-fun

[1 2 3 4 5] tolist x!
x tostr "1 2 3 4 5" test=
null tostr "" test=
5 null pair tostr "5" test=
5 6 null pair pair tostr "5 6" test=

##################################################

# a) function adds the object to the front of the list
lprepend: (l :List, x :Obj -> :List) {
    x l pair 
} fun

null 10 lprepend 20 lprepend 30 lprepend y!
y tostr "30 20 10" test=

# b) todo: checking if the list has atleast one string
has-string?: (l :List -> :Bool) {
    {l null?} {false}
    {l pair-first str?} {true}
    {true}{l pair-rest has-string?}
} cond-fun

x has-string? false test=
"a" null pair has-string? true test=
["a" 1] tolist has-string? true test=
[1 "a"] tolist has-string? true test=
[1 2] tolist has-string? false test=
[1 2 "b"] tolist has-string? true test=

# c) todo: checking if every element of the list a :Num
only-numbers?: (l :List -> :Bool) {
    {l null?} {true}
    {l pair-first num?} {l pair-rest only-numbers?}
    {true} {false}
} cond-fun

x only-numbers? true test=
null only-numbers? true test=
"a" null pair only-numbers? false test=
["a" 1] tolist only-numbers? false test=
[1 2] tolist only-numbers? true test=

# d) todo: returning a list with only the strings from the original list
keep-strings: (l :List -> :List) {
    {l null?} {null}
    {l pair-first str?} {l pair-first l pair-rest keep-strings pair}
    {true} {l pair-rest keep-strings}
} cond-fun

["hello" "world" 1 true] tolist x!
x keep-strings tostr "hello world" test=

test-stats